if (isBuildModule.toBoolean()) {
    //作为独立App应用运行
    apply plugin: 'com.android.application'
} else {
    //作为组件运行
    apply plugin: 'com.android.library'
}
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        //阿里路由框架配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        //TODO:注意这一行代码，目的是指定library的混淆文件，这样做的目的是，无需在app中添加混淆了,这个library中的混淆都添加在这个文件中即可
        //TODO:指定子模块的混淆文件
        consumerProguardFiles 'proguard-rules.pro'
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isBuildModule.toBoolean()) {
                //独立运行
                manifest.srcFile 'src/main/alone/AndroidManifest.xml'
            } else {
                //合并到宿主
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //正式版本时，排除alone文件夹下所有调试文件
                    exclude 'src/main/alone/*'
                }
            }
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file('TestKey')
            storePassword '123abc'
            keyAlias 'TestKeyAlias'
            keyPassword '123abc'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            //集成Google登录时一定要注意配置debug签名和firebase控制台中配置的签名文件一致
            //否则会出现一直获取不到用户信息
            signingConfig signingConfigs.release
        }
        release {
            //开启混淆
            minifyEnabled true
            //压缩优化
            zipAlignEnabled true
            //默认的混淆文件以及我们指定的混淆文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}


